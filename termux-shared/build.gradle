plugins {
    id 'com.android.library'
    id 'maven-publish'
}

android {
    namespace 'com.termux.shared'

    compileSdk project.properties.compileSdkVersion.toInteger()
    ndkVersion = System.getenv("JITPACK_NDK_VERSION") ?: project.properties.ndkVersion

    defaultConfig {
        minSdk project.properties.minSdkVersion.toInteger()
        targetSdk project.properties.targetSdkVersion.toInteger()

        // 推荐升级 Runner
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        externalNativeBuild {
            ndkBuild {
                cppFlags ''
            }
        }
    }

    buildFeatures {
        buildConfig true
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        // Flag to enable support for the new language APIs
        coreLibraryDesugaringEnabled true

        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    externalNativeBuild {
        ndkBuild {
            path file('src/main/cpp/Android.mk')
        }
    }
    publishing {
        singleVariant('release') {
            withSourcesJar()
        }
    }
}

dependencies {
    def markwonVersion = project.properties['markwonVersion'] ?: '4.6.2'

    implementation "androidx.appcompat:appcompat:1.6.1"
    implementation "androidx.annotation:annotation:1.7.0"
    implementation "androidx.core:core:1.9.0"
    implementation "androidx.window:window:1.2.0"
    implementation "androidx.window:window-java:1.2.0"
    implementation "com.google.android.material:material:1.10.0"
    implementation "com.google.guava:guava:31.0.1-jre"

    implementation "io.noties.markwon:core:$markwonVersion"
    implementation "io.noties.markwon:ext-strikethrough:$markwonVersion"
    implementation "io.noties.markwon:linkify:$markwonVersion"
    implementation "io.noties.markwon:recycler:$markwonVersion"

    implementation "org.lsposed.hiddenapibypass:hiddenapibypass:6.1"

    // Do not increment version higher than 1.0.0-alpha09 since it will break ViewUtils and needs to be looked into
    // noinspection GradleDependency
    // implementation "androidx.window:window:1.0.0-alpha09" 冲突，已被替代

    // Do not increment version higher than 2.5 or there
    // will be runtime exceptions on android < 8
    // due to missing classes like java.nio.file.Path.
    implementation "commons-io:commons-io:2.5"

    implementation project(":terminal-view")

    implementation "com.termux:termux-am-library:v2.0.0"

    testImplementation "junit:junit:4.13.2"
    androidTestImplementation "androidx.test.ext:junit:1.1.3"
    androidTestImplementation "androidx.test.espresso:espresso-core:3.4.0"
    coreLibraryDesugaring "com.android.tools:desugar_jdk_libs:2.0.3"
}

tasks.register("sourceJar", Jar) {
    archiveClassifier.set("sources")
    from android.sourceSets.main.java.srcDirs
}

afterEvaluate {
    publishing {
        publications {
            // Creates a Maven publication called "release".
            release(MavenPublication) {
                from components.release
                groupId = 'com.termux'
                artifactId = 'termux-shared'
                version = '0.118.0'
                artifact(sourceJar)
            }
        }
    }
}
