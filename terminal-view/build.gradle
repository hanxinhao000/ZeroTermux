plugins {
    id 'com.android.library'
    id 'maven-publish'
}

android {
    namespace 'com.termux.view'

    compileSdk project.properties.compileSdkVersion.toInteger()

    defaultConfig {
        minSdk project.properties.minSdkVersion.toInteger()
        targetSdk project.properties.targetSdkVersion.toInteger()
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildFeatures {
        buildConfig true
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    publishing {
        singleVariant('release') {
            withSourcesJar()
        }
    }
}

dependencies {
    implementation "androidx.annotation:annotation:1.7.0"
    api project(":terminal-emulator")
    testImplementation 'junit:junit:4.13.2'
}

tasks.register("sourceJar", Jar) {
    archiveClassifier.set("sources")
    from android.sourceSets.main.java.srcDirs
}

afterEvaluate {
    publishing {
        publications {
            bar(MavenPublication) {
                from components.release

                groupId 'com.termux'
                artifactId 'terminal-view'
                version "0.114"

                artifact(sourceJar)
            }
        }

        repositories {
            maven {
                name = "GitHubPackages"
                url = uri("https://maven.pkg.github.com/termux/termux-app")

                credentials {
                    username = System.getenv("GH_USERNAME")
                    password = System.getenv("GH_TOKEN")
                }
            }
        }
    }
}
