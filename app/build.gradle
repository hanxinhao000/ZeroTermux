plugins {
    id "com.android.application"
    id "org.jetbrains.kotlin.android"
    id "kotlin-kapt"
}

android {
    namespace "com.termux"
    compileSdk project.properties.compileSdkVersion.toInteger()

    buildFeatures {
        viewBinding true
        buildConfig true
    }

    lint {
        checkReleaseBuilds false
        abortOnError false
        disable 'ProtectedPermissions'
    }

    kotlinOptions {
        jvmTarget = "17"
    }

    tasks.withType(JavaCompile).configureEach {
        options.fork = true
        options.forkOptions.jvmArgs += ["-Duser.language=en"]
    }

    packaging {
        jniLibs {
            useLegacyPackaging = true
        }
        resources {
            excludes += ['META-INF/LICENSE', 'META-INF/DEPENDENCIES']
        }
    }

    signingConfigs {
        release {
            keyAlias System.getenv("KEY_ALIAS") ?: 'phone1'
            keyPassword System.getenv("KEY_PASSWORD") ?: '654321'
            storeFile rootProject.file("phone.jks")
            storePassword System.getenv("STORE_PASSWORD") ?: '123456'
        }
        debug {
            keyAlias System.getenv("KEY_ALIAS") ?: 'phone1'
            keyPassword System.getenv("KEY_PASSWORD") ?: '654321'
            storeFile rootProject.file("phone.jks")
            storePassword System.getenv("STORE_PASSWORD") ?: '123456'
        }
    }


    defaultConfig {
        applicationId "com.termux"
        minSdk project.properties.minSdkVersion.toInteger()
        targetSdk project.properties.targetSdkVersion.toInteger()

        versionCode 117
        versionName "0.118.3.49"

        manifestPlaceholders = [
            TERMUX_PACKAGE_NAME : "com.termux",
            TERMUX_APP_NAME : "Termux",
            TERMUX_API_APP_NAME : "Termux:API",
            TERMUX_BOOT_APP_NAME : "Termux:Boot",
            TERMUX_FLOAT_APP_NAME : "Termux:Float",
            TERMUX_STYLING_APP_NAME : "Termux:Styling",
            TERMUX_TASKER_APP_NAME : "Termux:Tasker",
            TERMUX_WIDGET_APP_NAME : "Termux:Widget"
        ]

        externalNativeBuild {
            ndkBuild {
                cFlags "-std=c11", "-Wall", "-Wextra", "-Werror", "-Os", "-fno-stack-protector", "-Wl,--gc-sections"
            }
        }



        ndk {
            abiFilters 'x86', 'x86_64', 'armeabi-v7a', 'arm64-v8a'
        }
    }



    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }

        debug {
            signingConfig signingConfigs.debug
        }
    }

    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    externalNativeBuild {
        ndkBuild {
            path "src/main/cpp/Android.mk"
        }
    }

    splits.abi {
        enable true
        reset()
        include "x86", "x86_64", "armeabi-v7a", "arm64-v8a"
        universalApk true
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }

}

dependencies {
    def libsuVersion = '5.2.1'
    def markwonVersion = '4.6.2'

    coreLibraryDesugaring "com.android.tools:desugar_jdk_libs:2.0.3"

    implementation fileTree(include: ['*.jar', '*.aar'], dir: 'libs')
    implementation "androidx.annotation:annotation:1.7.0"
    implementation "androidx.core:core-ktx:1.12.0"
    implementation "androidx.drawerlayout:drawerlayout:1.2.0"
    implementation "androidx.preference:preference:1.2.1"
    implementation "androidx.viewpager:viewpager:1.0.0"

    implementation "com.google.guava:guava:31.0.1-jre"
    implementation 'com.google.code.gson:gson:2.10.1'
    implementation 'com.lzy.net:okgo:3.0.4'
    //switch Btn
    implementation 'com.github.iielse:switchbutton:1.0.4'
    implementation 'com.github.getActivity:XXPermissions:18.5'
    implementation 'com.daimajia.numberprogressbar:library:1.4@aar'

    implementation 'me.laoyuyu.aria:core:3.8.16'
    kapt 'me.laoyuyu.aria:compiler:3.8.16'

    // The core module that provides APIs to a shell
    implementation "com.github.topjohnwu.libsu:core:${libsuVersion}"
    // Optional: APIs for creating root services. Depends on ":core"
    implementation "com.github.topjohnwu.libsu:service:${libsuVersion}"
    // Optional: Provides remote file system support
    implementation "com.github.topjohnwu.libsu:nio:${libsuVersion}"
    // debugImplementation 'com.github.getActivity:Logcat:9.6'
    implementation 'com.scottyab:rootbeer-lib:0.1.0'
    //OTG0
    implementation 'me.jahnen:libaums:0.8.0'
    implementation 'me.jahnen:libaums-httpserver:0.5.3'
    implementation 'me.jahnen:libaums-storageprovider:0.5.1'
    implementation 'uk.co.alt236:usbdeviceenumerator:2.0.0'
    //webview框架
    implementation 'com.just.agentweb:agentweb:4.0.2'
    implementation 'com.just.agentweb:download:4.0.2'
    //颜色取色器
    implementation 'com.github.rtugeek:colorseekbar:2.0.3'
    //图片/视频选择器
    implementation 'com.lcw.library:imagepicker:2.2.7'
    //滑动器
    implementation 'com.billy.android:smart-swipe:1.1.0'
    //compat for android support library
    implementation 'com.billy.android:smart-swipe-x:1.1.0'
    //glide
    implementation 'com.github.bumptech.glide:glide:4.16.0'
    kapt 'com.github.bumptech.glide:compiler:4.16.0'
    //状态栏
    //https://github.com/gyf-dev/ImmersionBar
    implementation 'com.geyifeng.immersionbar:immersionbar:3.2.2'
    //Zip解压
    implementation 'me.testica:codeeditor:1.0.2'
    //线性背景
    implementation 'com.googlecode.plist:dd-plist:1.16'
    implementation 'com.tananaev:adblib:1.3'
    implementation 'org.samba.jcifs:jcifs:1.3.3'
    implementation group: 'commons-fileupload', name: 'commons-fileupload', version: '1.3.1'
    implementation 'org.nanohttpd:nanohttpd:2.3.1'
    compileOnly group: 'javax.servlet', name: 'servlet-api', version: '2.5'
    implementation 'com.hzy:un7zip:+'

    //ZFlie
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
    implementation 'com.mallotec.reb:plugin-locale:1.0.12'

    implementation "io.noties.markwon:core:${markwonVersion}"
    implementation "io.noties.markwon:ext-strikethrough:${markwonVersion}"
    implementation "io.noties.markwon:linkify:${markwonVersion}"
    implementation "io.noties.markwon:recycler:${markwonVersion}"

    implementation("org.jetbrains.kotlinx:kotlinx-coroutines-core:1.7.3")
    implementation('cn.hotapk:fastandrutils:0.8.0') {
        exclude group: 'com.android.support'
    }

    implementation project(":terminal-view")
    implementation project(":termux-shared")
    //implementation project(":FHttpServer")
    //implementation project(":z_file")
    implementation 'androidx.cardview:cardview:1.0.0'
    //implementation project(path: ':xh_lib')
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.10.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'

    implementation 'com.google.zxing:core:3.3.3'

    implementation files('libs/commons-logging-1.2.jar')
    implementation files('libs/commons-net-3.8.0.jar')
    implementation files('libs/ftplet-api-1.1.1.jar')
    implementation files('libs/ftpserver-core-1.1.1-edited.jar')
    implementation files('libs/jlan-5.jar')
    implementation files('libs/log4j-1.2.17.jar')
    implementation files('libs/mina-core-2.0.16.jar')
    implementation files('libs/slf4j-api-1.7.21.jar')
    implementation files('libs/slf4j-log4j12-1.7.21.jar')
    implementation files('libs/commons-codec-1.10-rep.jar')

    testImplementation "junit:junit:4.13.2"
    testImplementation "org.robolectric:robolectric:4.4"
}

task versionName {
    doLast {
        print android.defaultConfig.versionName
    }
}

def downloadBootstrap(String arch, String expectedChecksum, String version) {
    def digest = java.security.MessageDigest.getInstance("SHA-256")

    def localUrl = "src/main/cpp/bootstrap-" + arch + ".zip"
    def file = new File(projectDir, localUrl)
    if (file.exists()) {
        def buffer = new byte[8192]
        def input = new FileInputStream(file)
        while (true) {
            def readBytes = input.read(buffer)
            if (readBytes < 0) break
            digest.update(buffer, 0, readBytes)
        }
        def checksum = new BigInteger(1, digest.digest()).toString(16)
        if (checksum == expectedChecksum) {
            return
        } else {
            logger.quiet("Deleting old local file with wrong hash: " + localUrl)
            file.delete()
        }
    }

    def remoteUrl = "https://github.com/termux/termux-packages/releases/download/bootstrap-" + version + "/bootstrap-" + arch + ".zip"
    logger.quiet("Downloading " + remoteUrl + " ...")

    file.parentFile.mkdirs()
    def out = new BufferedOutputStream(new FileOutputStream(file))

    def connection = new URL(remoteUrl).openConnection()
    connection.setInstanceFollowRedirects(true)
    def digestStream = new java.security.DigestInputStream(connection.inputStream, digest)
    out << digestStream
    out.close()

    def checksum = new BigInteger(1, digest.digest()).toString(16)
    if (checksum != expectedChecksum) {
        file.delete()
        throw new GradleException("Wrong checksum for " + remoteUrl + ": expected: " + expectedChecksum + ", actual: " + checksum)
    }
}

clean {
    doLast {
        def tree = fileTree(new File(projectDir, 'src/main/cpp'))
        tree.include 'bootstrap-*.zip'
        tree.each { it.delete() }
    }
}

task downloadBootstraps() {
    doLast {
        def version = "2025.08.24-r1+apt.android-7"
        downloadBootstrap("aarch64", "a2de41340f145686d946e56858df7a3f1a176b85a2e3cc922331a8f79d4c46e4", version)
        downloadBootstrap("arm", "c3aa9abff607b7e55fe05a15e7f4544b321899dfeaf938912f86d82b46a3dfbe", version)
        downloadBootstrap("i686", "bc7e4a0a8d78cf7d3a6fe28aff7e0ae0bdf3e8c8197e0a95850e02cf48652ac9", version)
        downloadBootstrap("x86_64", "390d8383803c7b97781c5532a7d56749b4cc3e0b468d5f8d5c85720fa780cfcb", version)
    }
}


afterEvaluate {
    android.applicationVariants.all { variant ->
        variant.javaCompileProvider.get().dependsOn(downloadBootstraps)
    }
}
repositories {
    mavenCentral()
}
